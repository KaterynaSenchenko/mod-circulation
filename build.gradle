apply plugin: 'java'
apply plugin: 'application'

mainClassName = "org.folio.circulation.Launcher"
version = "3.0.0-SNAPSHOT"

ext.sharedManifest = manifest {
  attributes(
    "Main-Class": mainClassName,
    "Implementation-Title": "circulation",
    "Implementation-Version": version)
}

repositories {
  mavenCentral()
}

dependencies {
  //Don't use the code gen directly but hoping to get rid of warnings in build
  compile 'io.vertx:vertx-codegen:3.3.3'
  compile 'io.vertx:vertx-core:3.3.3'
  compile 'io.vertx:vertx-web:3.3.3'
  compile 'org.apache.httpcomponents:httpclient-osgi:4.5.2'

  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
  testCompile 'joda-time:joda-time:2.9.7'
  testCompile 'org.apache.commons:commons-lang3:3.0'
}

compileJava {
  options.compilerArgs += ["-Xlint:all"]
}

compileTestJava {
  options.compilerArgs += ["-Xlint:all"]
}

test {
  //Gradle runs tests designed to be run in a suite twice
  //See: https://discuss.gradle.org/t/how-to-prevent-junit-test-suite-classes-from-being-ran-twice/7107/4
  exclude '**/api/**Tests.class'
}

task testApiViaOkapi(type: Test) {
  include '**/api/APITestSuite.class'

  systemProperty 'use.okapi.initial.requests', 'true'
  systemProperty 'use.okapi.storage.requests', 'true'

  def okapiAddress = System.getProperty('okapi.address') ?: 'http://localhost:9130'

  systemProperty 'okapi.address', okapiAddress
}

task testStorageViaOkapi(type: Test) {
  include '**/api/APITestSuite.class'

  systemProperty 'use.okapi.initial.requests', 'false'
  systemProperty 'use.okapi.storage.requests', 'true'

  def okapiAddress = System.getProperty('okapi.address') ?: 'http://localhost:9130'

  systemProperty 'okapi.address', okapiAddress
}

jar {
  manifest = project.manifest {
    from sharedManifest
  }
}

task fatJar(type: Jar) {
  manifest = project.manifest {
    from sharedManifest
  }
  archiveName 'mod-circulation.jar'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.3'
}
